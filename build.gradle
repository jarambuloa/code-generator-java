plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'application'
	id "org.openapi.generator" version "7.5.0"
}

group = 'com.jarambulo'
version = '0.0.1-SNAPSHOT'
description = 'Generador code project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations {
	generatorRuntime
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.yaml:snakeyaml:2.2'

	implementation 'gg.jte:jte:3.1.6'
	implementation 'gg.jte:jte-runtime:3.1.6'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'

	//validacion structural (Been validation)
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

}

tasks.named('test') {
	useJUnitPlatform()
}

application {
	mainClass = 'com.jarambuloa.codegeneratorjava.generator.ApplicationGenerator'
}

/* =========================================================
   TASK 1 — Generar openapi.yaml (AISLADO)
   ========================================================= */
tasks.register("generateOpenApiSpec", JavaExec) {
	group = "code generation"
	description = "Genera openapi.yaml desde el DSL"

	mainClass = "com.jarambuloa.codegeneratorjava.generator.ApplicationGenerator"
	classpath = sourceSets.main.runtimeClasspath
	workingDir = projectDir

	inputs.dir("src/main/java")
	inputs.dir("src/main/resources")
	outputs.file(layout.buildDirectory.file("generated/openapi.yaml"))
}


/* =========================================================
   EXTENSIÓN OpenAPI Generator (SOLO CONFIG)
   ========================================================= */
openApiGenerate {
	generatorName = "spring"
	inputSpec = "$buildDir/generated/openapi.yaml"
	outputDir = "$buildDir/generated/openapi"

	apiPackage = "com.empresa.usuarios.adapters.in.rest.api"
	modelPackage = "com.empresa.usuarios.adapters.in.rest.dto"

	configOptions = [
			interfaceOnly  : "true",
			useSpringBoot3 : "true",
			useTags        : "true",
			dateLibrary    : "java17"
	]
}

/* =========================================================
   TASK 2 — Orquestador (SIN CICLOS)
   ========================================================= */
tasks.register("buildWithOpenApi") {
	group = "build"
	description = "Genera OpenAPI, interfaces y compila"

	dependsOn(
			"generateOpenApiSpec",
			"openApiGenerate",
			"compileJava"
	)
}
